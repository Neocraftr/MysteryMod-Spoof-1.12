buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:3.+"
        classpath "de.undercouch:gradle-download-task:4.1.1"
    }
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "de.undercouch.download"

version = "1.0"
group = "de.neocraftr.mmspoof"
archivesBaseName = "MysteryModSpoof"

minecraft {
    mappings channel: "snapshot", version: "20171003-1.12"

    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2855"
    compile files("${rootDir.getAbsolutePath()}/libs/lm_api_mc1.12.2.jar")
}

task updateLabymodApi() {
    final out = file("${rootDir.getAbsolutePath()}/libs/lm_api_mc1.12.2.jar")
    outputs.file(out)
    outputs.upToDateWhen {false}

    doLast {
        download {
            src "http://dl.labymod.net/latest/api/files/lm_api_mc1.12.2.jar"
            dest out
            onlyIfModified true
        }
    }
}

jar.finalizedBy("reobfJar")

compileJava {
    dependsOn updateLabymodApi
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}
